import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id 'java'
    id 'signing'
    id "com.vanniktech.maven.publish" version "0.28.0"
}

group = 'me.diademiemi'
version = '0.1'

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    // GeyserMC's repository
    maven {
        url = uri("https://repo.opencollab.dev/main/")
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.13-R0.1-SNAPSHOT"
    compileOnly "org.geysermc.geyser:api:2.2.0-SNAPSHOT"
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}
tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

// Spigot
processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}



ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}

publishing {
    repositories {
        maven {
            name = "GitHub"
            url = uri("https://maven.pkg.github.com/diademiemi/invuilib")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

mavenPublishing {
    coordinates("me.diademiemi", "invuilib", "0.1")

    pom {
        name = "InvUILib"
        description = "An Inventory GUI Library for Spigot"
        inceptionYear = "2024"
        url = "https://github.com/diademiemi/InvUILib/"
        licenses {
            license {
                name = "MIT"
                url = "https://opensource.org/licenses/MIT"
            }
        }
        developers {
            developer {
                id = "diademiemi"
                name = "Jasmine Emilia"
                url = "https://diademiemi.me/"
            }
        }
        scm {
            url = "https://github.com/diademiemi/InvUILib/"
            connection = "scm:git:git://github.com/diademiemi/InvUILib.git"
            developerConnection = "scm:git:ssh://git@github.com/diademiemi/InvUILib.git"
        }
    }
    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL, true)

    signAllPublications()

}
